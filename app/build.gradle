apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'androidx.navigation.safeargs.kotlin'

android {
    compileSdk rootProject.ext.compileSdk

    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    }
    else {
        throw new GradleException("Could not read version.properties!")
    }

    def versionMajor = versionProps['VERSION_MAJOR'].toInteger()
    def versionMinor = versionProps['VERSION_MINOR'].toInteger()
    def versionPatch = versionProps['VERSION_PATCH'].toInteger()
    def versionBuild = versionProps['VERSION_BUILD'].toInteger()
    def versionNumber = versionProps['VERSION_NUMBER'].toInteger()
    def vName = "${rootProject.ext.applicationName}.${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdk rootProject.ext.minSdk
        targetSdk rootProject.ext.targetSdk
        versionCode versionNumber
        versionName vName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            resValue("string","version_text",vName + "(release)")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string","version_text",vName + "(debug)")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':core')
    implementation project(':navigation')
    implementation 'com.github.PierfrancescoSoffritti:AndroidYouTubePlayer:10.0.5'
    implementation 'kr.co.prnd:readmore-textview:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task updateVersion {
    doLast {
        println "Updating version"

        def versionPropsFile = file("version.properties")
        Properties versionProps = new Properties()
        if (versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))
        }
        else {

            throw new GradleException("Could not read version.properties!")
        }

        def versionMajor = versionProps["VERSION_MAJOR"].toInteger()
        def versionMinor = versionProps["VERSION_MINOR"].toInteger()
        def versionPatch = versionProps["VERSION_PATCH"].toInteger()
        def versionBuild = versionProps["VERSION_BUILD"].toInteger()
        def versionNumber = versionProps["VERSION_NUMBER"].toInteger() + 1

        if (versionBuild < 20) {
            versionBuild++
        } else {
            versionBuild = 0
            if (versionPatch < 10) {
                versionPatch++
            } else {
                versionPatch = 0
                if (versionMinor < 15) {
                    versionMinor++
                } else {
                    versionMinor = 0
                    versionMajor++
                }
            }
        }

        versionProps["VERSION_NUMBER"] = versionNumber.toString()
        versionProps["VERSION_MAJOR"] = versionMajor.toString()
        versionProps["VERSION_MINOR"] = versionMinor.toString()
        versionProps["VERSION_PATCH"] = versionPatch.toString()
        versionProps["VERSION_BUILD"] = versionBuild.toString()

        versionPropsFile.withWriter { versionProps.store(it, null) }

        println "New version $versionNumber -> $versionMajor.$versionMinor.$versionPatch.$versionBuild"
    }

}
